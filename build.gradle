buildscript {
    repositories {
        mavenCentral()
        maven {
          url "https://plugins.gradle.org/m2/"
        }
        eventuateMavenRepoUrl.split(',').each { repoUrl -> maven { url repoUrl } }
    }
    dependencies {
        classpath "io.eventuate.plugins.gradle:eventuate-plugins-gradle-publish:$eventuatePluginsGradleVersion"
    }
}


allprojects {
    group = "io.eventuate.tram.springwolf"
}

apply plugin: "io.eventuate.plugins.gradle.publish.EventuatePublish"

allprojects {

    repositories {
        mavenCentral()
        eventuateMavenRepoUrl.split(',').each { repoUrl -> maven { url repoUrl } }
    }

    if (project.name.endsWith("-bom")) {
        apply plugin: 'java-platform'
    } else {

        apply plugin: 'java-library'

        java {
            toolchain {
                languageVersion.set(JavaLanguageVersion.of(17))
            }
        }
        dependencies {
            testImplementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
        }
        test {
            useJUnitPlatform()
        }

    }
}

tasks.register('copySpringwolfDocs') {
    group = 'documentation'
    description = 'Copies springwolf.json files from build directories to example-asyncapi-docs'

    subprojects.each { subproject ->
        if (!subproject.name.endsWith('-starter') && !subproject.name.endsWith('-bom')) {
            dependsOn "${subproject.path}:test"
        }
    }

    doLast {
        def targetDir = project.file('example-asyncapi-docs')
        targetDir.deleteDir()
        targetDir.mkdirs()

        subprojects.each { subproject ->
            if (!subproject.name.endsWith('-starter')) {
                def springwolfFiles = subproject.fileTree(dir: 'build', include: 'springwolf*.json')
                springwolfFiles.each { springwolfFile ->
                    if (springwolfFile.exists()) {
                        def shortName = subproject.name.replaceAll('eventuate-tram-springwolf-support-', '')
                                .replaceAll('-springwolf-support', '')
                        def originalFileName = springwolfFile.name
                        def suffix = originalFileName.replace('springwolf', '')
                        copy {
                            from springwolfFile
                            into targetDir
                            rename { String fileName ->
                                "${shortName}${suffix}"
                            }
                        }
                    }
                }
            }
        }
    }
}

gradle.projectsEvaluated {
    tasks.register('aggregateJavaDocs', Javadoc) {
        description = 'Aggregated Javadoc API documentation of all subprojects.'
        group = JavaBasePlugin.DOCUMENTATION_GROUP

        destinationDir file("$buildDir/docs/javadoc")
    }

    tasks.register('aggregateJavaDocsJar', Jar) {
        dependsOn project.aggregateJavaDocs
//        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    artifacts {
        archives project.aggregateJavaDocsJar
    }

    javadocJar.dependsOn(aggregateJavaDocs)
    generateMetadataFileForMavenPublication.dependsOn(project.aggregateJavaDocsJar)

}

//subprojects {
//    configurations.all {
//        resolutionStrategy.cacheChangingModulesFor 10, 'minutes'
//    }
//}
